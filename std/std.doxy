class Void;

primitive Char {
    extern fn == (self, other: Char) Bool,
    fn != (self, other: Char) Bool {
        return !(self == other)
    }

    extern fn as_int (self) Int,
    extern fn str (self) Str,

    extern fn from_int (i: Int) Char,
}

primitive Bool {
    extern fn || (self, other: Bool) Bool,
    extern fn && (self, other: Bool) Bool,

    fn str (self) Str {
        if self {
            return "true"
        }
        return "false"
    }
}

primitive Int {
    extern fn +  (self, n: Int) Int,
    extern fn -  (self, n: Int) Int,
    extern fn *  (self, n: Int) Int,
    extern fn /  (self, n: Int) Int,
    extern fn %  (self, n: Int) Int,
    extern fn == (self, n: Int) Bool,
    extern fn != (self, n: Int) Bool,
    extern fn <  (self, n: Int) Bool,
    extern fn <= (self, n: Int) Bool,
    extern fn >  (self, n: Int) Bool,
    extern fn >= (self, n: Int) Bool,

    fn max (self, other = 9223372036854775807) Int {
        if self > other {
            return self
        }
        return other
    }

    fn min (self, other = -9223372036854775807 - 1) Int {
        if self < other {
            return self
        }
        return other
    }

    fn abs (self) Int {
        if self < 0 {
            return -self
        }
        return self
    }

    extern fn str (self) Str,
}

class Str {
    extern fn == (self, other: Str) Bool,
    extern fn != (self, other: Str) Bool,
    extern fn +  (self, other: Str) Str,

    extern fn at (self, i: Int) Char,
    extern fn at_raw (self, i: Int) Char,

    fn concat (self, other: Str) Str {
        return self + other
    }

    fn contains (self, other: Str) Bool {
        return self.find(other) >= 0
    }

    fn find (self, other: Str) Int {
        if other.len() == 0 {
            return 0
        }
        let mut i = 0;
        while i < self.len() {
            if self.at(i) == other.at(0) {
                let mut j = 1;
                while j < other.len() && self.at(i + j) == other.at(j) {
                    j = j + 1;
                }
                if j == other.len() {
                    return i
                }
            }
            i = i + 1
        }
        return -1
    }

    extern fn len (self) Int,

    fn repeat (self, n: Int) Str {
        if n < 1 {
            return ""
        }
        // TODO: helper function in assembly to do this without allocating
        //       a new string each time through the loop
        return self + self.repeat(n - 1)
    }

    fn str (self) Str {
        return self
    }

    fn substr (self, start = 0, end = 9223372036854775807) Str {
            // TODO: give the end a proper default value.
            // This will work in all cases (end = 2^63-1 = max Int),
            // but it's not pretty.
        if self.len() < 1 {
            return ""
        }
        if end < 0 {
            return self.substr(start, Int.max(self.len() + end, 0))
        }
        if start < 0 {
            return self.substr(Int.max(self.len() + start, 0), end)
        }
        if start >= end {
            return ""
        }
        let actual_end = Int.min(self.len(), end);
        let mut i = Int.max(start, 0);
        let mut s = "";
        while i < actual_end {
            // use Str.at_raw here as bounds checking has already been done
            // and we don't want to do it again every iteration
            s = s + self.at_raw(i).str();
            i = i + 1
        }
        return s
    }

    fn starts_with (self, other: Str) Bool {
        if other.len() > self.len() {
            return false
        }
        let mut i = 0;
        while i < other.len() {
            if self.at(i) != other.at(i) {
                return false
            }
            i = i + 1
        }
        return true
    }

    fn ends_with (self, other: Str) Bool {
        if other.len() > self.len() {
            return false
        }
        let mut i = 0;
        while i < other.len() {
            if self.at(self.len() - i - 1) != other.at(other.len() - i - 1) {
                return false
            }
            i = i + 1
        }
        return true
    }

    fn reversed (self) Str {
        let mut i = 0;
        let mut s = "";
        while i < self.len() {
            s = self.at(i).str() + s;
            i = i + 1
        }
        return s
    }

    fn replace (self, search: Str, replace_with = "", max = -1) Str {
        if search.len() == 0 {
            return self
        }
        if max == 0 {
            return self
        }
        let mut i = 0;
        let mut s = "";
        let mut replacements = 0;
        while i < self.len() {
            if self.at(i) == search.at(0) {
                let mut j = 1;
                while j < search.len() && self.at(i + j) == search.at(j) {
                    j = j + 1;
                }
                if j == search.len() {
                    s = s + replace_with;
                    i = i + j;
                    replacements = replacements + 1;
                    if max >= 0 && replacements >= max {
                        s = s + self.substr(i);
                        break
                    }
                    continue
                }
            }
            s = s + self.at(i).str();
            i = i + 1
        }
        return s
    }

    fn insert (self, index: Int, other: Str) Str {
        if index < 0 {
            return self.insert(self.len() + index + 1, other)
        }
        if index >= self.len() {
            return self + other
        }
        return self.substr(0, index) + other + self.substr(index)
    }

    fn remove (self, index: Int, count = 1) Str {
        //println("remove: " + self + ", " + index.str() + ", " + count.str());
        if index < 0 {
            if self.len() + index < 0 {
                return self.substr(self.len() + index + count)
            }
            return self.remove(self.len() + index, count)
        }
        if index >= self.len() {
            return self
        }
        if count < 1 {
            return self
        }
        return self.substr(0, index) + self.substr(index + count)
    }

    extern fn utf8_size (self) Int,
    extern fn from_utf8 (utf8: Str) Str
}

class Time {
	seconds: Int,
	microseconds: Int,

	fn - (self, other: Time) Time {
	    let self_micros = self.seconds * 1000000 + self.microseconds;
	    let other_micros = other.seconds * 1000000 + other.microseconds;
	    let diff_micros = self_micros - other_micros;
	    return new Time {
	        seconds: diff_micros / 1000000,
	        microseconds: diff_micros % 1000000
	    }
	}

	fn + (self, other: Time) Time {
	    let self_micros = self.seconds * 1000000 + self.microseconds;
	    let other_micros = other.seconds * 1000000 + other.microseconds;
	    let sum_micros = self_micros + other_micros;
	    return new Time {
	        seconds: sum_micros / 1000000,
	        microseconds: sum_micros % 1000000
	    }
	}

	fn str (self) Str {
		return self.seconds.str() + "." + self.microseconds.str()
	}

	extern fn current_seconds () Int,
	extern fn current_microseconds () Int,
	fn now () Time {
		return new Time {
			seconds: Time.current_seconds(),
			microseconds: Time.current_microseconds()
		}
	}

	fn epoch () Time {
		return new Time {
			seconds: 0,
			microseconds: 0
		}
	}
}

class RawCmp <A, B> {
	val: A,
	extern fn eq (self, b: B) Bool,
}

class Option <A> {
	value: A,

	extern fn none () Option<A>,
	fn some (value: A) Option<A> {
		return new Option {
			value: value
		}
	}

	fn is_ok (self) Bool {
		return !self.is_none()
	}

	fn is_none (self) Bool {
		return new RawCmp <A, Int> { val: self.value }
               		.eq(0)
	}

	fn unwrap (self) A {
		if self.is_none() {
			exit(1)
		}
		return self.value
	}
}

primitive Ptr <T>;

class List <T> {
	start: T,
    size: Int,

    fn at_option (self, i: Int) Option<T> {
		if i < 0 || i >= self.size {
			return Option.none()
		}
		return Option.some(self.at_raw(i))
    }
    fn at (self, i: Int) T {
		if i < 0 || i >= self.size {
			panic("Index out of bounds (" + i.str() + ")")
		}
		return self.at_raw(i)
	}
    extern fn at_raw (self, i: Int) T
}

extern fn exit (code: Int = 0);
extern fn input (prompt: Str = "", buffer_size: Int = 1000) Str;
extern fn print (msg: Str);
fn println (msg: Str, line_end="\n") {
    print(msg);
    print(line_end);
}
extern fn print_nl ();

fn panic (msg: Str) {
	println("PANIC: '" + msg + "'");
	exit(1);
}